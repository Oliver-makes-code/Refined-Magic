plugins {
	id 'org.quiltmc.loom' version '0.12.23'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group
qsl_version = project.qsl_version
qfapi_version = project.qfapi_version

repositories {
	// Quilt maven
	maven {
		name 'Quilt'
		url 'https://maven.quiltmc.org/repository/snapshot'
	}
	maven {
		name 'Quilt Release'
		url 'https://maven.quiltmc.org/repository/release'
	}
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
}

String qsl(String name) {
	return "org.quiltmc.qsl.${name}:${qsl_version}+${minecraft_version}-SNAPSHOT"
}

String qfapi(String name) {
	return "org.quiltmc.quilted-fabric-api:${name}:${qfapi_version}-${minecraft_version}-SNAPSHOT"
}
dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings(loom.layered {
		it.officialMojangMappings {
			setNameSyntheticMembers(false)
		}
		it.addLayer(quiltMappings.mappings("org.quiltmc:quilt-mappings:${project.minecraft_version}+build.${project.quilt_mappings}:v2"))
	})
	modImplementation "org.quiltmc:quilt-loader:${project.loader_version}"

	modImplementation include(qsl("core:qsl_base"))
	modImplementation include(qsl("core:registry"))
	modImplementation include(qsl("core:lifecycle_events"))
	modImplementation include(qsl("core:networking"))
	modImplementation include(qsl("core:resource_loader"))
	modImplementation include(qsl("item:item_group"))
	modImplementation include(qsl("block:block_extensions"))
	modImplementation include(qfapi("fabric-api-base"))
	modImplementation include(qfapi("fabric-registry-sync-v0"))
	modImplementation include(qfapi("fabric-networking-api-v1"))
	modImplementation include(qfapi("fabric-biome-api-v1"))
}

processResources {
	inputs.property "version", project.version

	filesMatching("quilt.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}
